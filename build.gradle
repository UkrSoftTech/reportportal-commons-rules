buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

}

plugins {
    id 'net.researchgate.release' version '2.4.1'
    id "com.jfrog.bintray" version "1.7"
    id "io.spring.dependency-management" version "0.6.0.RELEASE"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

description = 'EPAM Report portal. Report Portal Validation and Error Handling Commons'

repositories {
    mavenCentral()
    maven { url  "http://dl.bintray.com/epam/reportportal" }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:1.4.0.RELEASE"
    }
}

dependencies {
    compile 'com.epam.reportportal:commons-model:2.6.0'

    compile 'com.google.guava:guava:19.0'

    compile 'org.springframework:spring-webmvc'
    compile 'org.springframework.data:spring-data-mongodb'
    compile 'org.springframework.security:spring-security-core'
    compileOnly 'javax.servlet:javax.servlet-api'


    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'javax.servlet:javax.servlet-api'

}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId 'com.epam.reportportal'
            artifactId "$project.name"
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'reportportal'
        name = "$project.name"
        desc = "$description"
        websiteUrl = 'https://reportportal.github.io/'
        issueTrackerUrl = "https://github.com/reportportal/$project.name/issues"
        vcsUrl = "https://github.com/reportportal/$project.name"
        githubRepo = "reportportal/$project.name"
        githubReleaseNotesFile = 'CHANGELOG.md'

        userOrg = 'epam'
        licenses = ['GPL-3.0']
    }
}
afterReleaseBuild.dependsOn bintrayUpload
